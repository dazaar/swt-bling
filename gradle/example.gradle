sourceSets {
  examples {
    compileClasspath += main.output
    runtimeClasspath += main.output
  }
}

configurations {
  examplesCompile.extendsFrom compile
  examplesRuntime.extendsFrom runtime
}

dependencies {
  compile 'org.reflections:reflections:0.9.9-RC1'
}

idea {
  module {
    sourceDirs += file('src/examples/java')
    scopes.COMPILE.plus += configurations.examplesCompile
  }
}

tasks.addRule("Pattern: runExample<WidgetName>") { String taskName ->
  if (taskName.startsWith("runExample")) {
    task(taskName, type: JavaExec) {
      ext.sleakDebug = ""
      if (taskName.endsWith("Sleak")) {
        sleakDebug = "sleakDebug"
      }

      ext.widgetName = taskName - "runExample" - "Sleak"

      classpath = sourceSets.examples.runtimeClasspath
      main = "com.readytalk.examples.swt.Examples"
      args "${widgetName}", "${sleakDebug}"

      if (platform == 'darwin') {
        jvmArgs '-XstartOnFirstThread'
      }
    }
  }
}
